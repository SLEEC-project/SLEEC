grammar circus.robocalc.sleec.SLEEC with org.eclipse.xtext.common.Terminals

generate sLEEC "http://www.robocalc.circus/sleec/SLEEC"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Specification:
	defBlock=Defblock ruleBlock=RuleBlock
;

Defblock:
	'def_start' definitions+=Definition+ 'def_end'
;

Definition:
	{Event} 'event' name=EventID | {Measure} 'measure' name=MeasureID ':' type=Type | {Constant} 'constant' name=ConstID '=' value=Value
;

Value:
	int=INT | float=FLOAT | value=[Constant]
;

Type:
	{Boolean} 'boolean' | {Numeric} 'numeric' | {Scale} 'scale' '(' scaleParams+=ScaleParam (',' scaleParams+=ScaleParam)* ')'
;

RuleBlock:
	'rule_start' rules+=Rule+ 'rule_end'	
;

Rule:
	name=QualifiedName 'when' trigger=Trigger 'then' response=Response defeaters+=Defeater*
;

Trigger:
	event=[Event] ('and' expr=MBoolExpr)?
;

MBoolExpr:
	BoolComp
;

BoolComp returns MBoolExpr:
	Not ({BoolComp.left=current} op=BoolOp right=Not)*
;

Not returns MBoolExpr:
	{Not} 'not' left=RelComp | RelComp
;

RelComp returns MBoolExpr:
	Atom ({RelComp.left=current} op=RelOp right=Atom)*
;

Atom returns MBoolExpr:
	// Avoid using cross reference here as ScaleParam will never be matched as it is the same as Measure
	// ->measure=[Measure] | '(' MBoolExpr ')' | ->value=Value | ->scaleParam=[ScaleParam]
	name=ID | '(' MBoolExpr ')' | value=Value
;

Response:
	name=[Event] ('within' time=Value ('otherwise' response=Response)?)? | not?='not' name=[Event] 'within' time=Value
;

Defeater:
	'unless' expr=MBoolExpr ('then' response=Response)?
;

enum RelOp:
	LESS_THAN='<' | GREATER_THAN='>' | NOT_EQUAL='<>' | LESS_EQUAL='<=' | GREATER_EQUAL='>=' | EQUAL='='
;

enum BoolOp:
	AND='and' | OR='or'
;

QualifiedName:
	ID ('.' ID)*
;

EventID:
	ID
;

MeasureID:
	ID
;

ConstID:
	ID
;

ScaleParam:
	ID
;

terminal FLOAT returns ecore::EFloat:
	INT '.' INT
;
